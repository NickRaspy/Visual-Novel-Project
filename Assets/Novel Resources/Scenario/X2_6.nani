# X2_6
@back Space_1
@stopBgm
@bgm Space_1
# X2_6_AfterDialogue
@hidePrinter 
@showUI MainUI
@if main_quest_progress!=4
@choice  button:Buttons/Characters/RectButton pos:-500,0 handler:ButtonArea goto:.X2_6_Dialogue_1
@else
@choice  button:Buttons/Characters/RectButton pos:-500,0 handler:ButtonArea goto:.X2_6_Dialogue_2
@choice  button:Buttons/Characters/CiriButton pos:500,0 handler:ButtonArea goto:.X2_6_Dialogue_2
@endif
@stop

# X2_6_Dialogue_1
@hideUI MainUI
@char rect
rect: M? [wait i1] Weren't you suppose to go to the Y2-8?
# X2_6_Dialogue_1_Prechoice
@choice "Where am I?" goto:.X2_6_Dialogue_1_Choice_1
@choice "Where is Y2-8?" goto:.X2_6_Dialogue_1_Choice_2
@choice "Who are you?" goto:.X2_6_Dialogue_1_Choice_3
@choice "Yeah... right."
@stop
@hide rect
@goto .X2_6_AfterDialogue

# X2_6_Dialogue_1_Choice_1
rect: You are on X2-6 right now.
@goto .X2_6_Dialogue_1_Prechoice
# X2_6_Dialogue_1_Choice_2
rect: Somewhere far. You will find it eventually.
@goto .X2_6_Dialogue_1_Prechoice
# X2_6_Dialogue_1_Choice_3
rect: I'm Rect, the Rectangular!
rect: ...don't you have memory issues?
@goto .X2_6_Dialogue_1_Prechoice

# X2_6_Dialogue_2
@hideUI MainUI
@char rect pos:20,0
@char ciri pos:80,0
@if hadFinalDialog
@goto .X2_6_Dialogue_Final_Choice
@endif
rect: ...and you actually told him about Key of Wisdom...
ciri: Why wouldn't I? Should he know about it?
rect: Because he could use it for yourself!
ciri: Relax, he doesn't know how to u-
...
ciri: Oh, hi {playerName}!
rect: Did you bring Key of Wisdom?
rect: If yes... give it to me, please!
@char ciri.Angry
ciri: WHAT?!
ciri: Don't give it to him! You should give it to me!
ciri: This bounded creature doesn't deserve any wisdom! Why would he even need it?
@char rect.Angry
rect: Excuse me? "Bounded creature"?!
rect: Your sense of freedom made you greedy!
ciri: Grh...
(Both) GIVE IT TO ME!!!
# X2_6_Dialogue_Final_Choice
@if hasWisdomKey
@choice "(Give it to Rect)" goto:.X2_6_Dialogue_2_Choice_Rect
@choice "(Give it to Ciri)" goto:.X2_6_Dialogue_2_Choice_Ciri
@endif
@choice "Both of you doesn't deserve it!" goto:.X2_6_Dialogue_2_Choice_None
@choice "Give me a moment..." goto:.X2_6_Dialogue_2_Choice_Wait
@set hadFinalDialog=true
@stop

# X2_6_Dialogue_2_Choice_Rect
@char rect.Blushed
rect: Wait, seriously?
rect: Can't believe I have the Key...
@char ciri.Angry
ciri: Why...
ciri: You didn't understand what I said...
ciri: Screw it! I'll find everything I wanted by myself!
ciri: Be a nice friend to that closed in understanding the world mess!
@goto .Finale
# X2_6_Dialogue_2_Choice_Ciri
@char ciri.Blush
ciri: I knew it, I knew it!
ciri: Thank you so much!
ciri: Let's together explore the universe and find some new things!
@char rect.Angry
rect: Dude...[wait i0.3] why...
rect: I was the one who gave you direction...
rect: And yet you helped her...
rect: I thought you could be my friend...
@goto .Finale
# X2_6_Dialogue_2_Choice_None
rect: Huh?
ciri: What?
rect: Are you kidding me?
rect: I told you Ciri! We shouldn't tell him what was that!
ciri: Dang it...[wait i0.3] you were right...
@char rect.Normal
@char ciri.Normal
ciri: You know... have it or not - doesn't matter.
ciri: I think we don't need it. Rect, we should get what we want only by our actions and will.
ciri: This item is a simple way, yet can be boring.
rect: You are right, Ciri. Maybe I shouldn't be that restrained even with 4 of my sides.
rect: Let's find our way together.
rect: And {playerName}. Use it to find the way home.
@goto .Finale
# X2_6_Dialogue_2_Choice_Wait
ciri: Well hurry up!
rect: And don't forget to give it to me!
@hideChars
@goto .X2_6_AfterDialogue

# Finale
The End.
Thanks for playing this trial project!
@title
